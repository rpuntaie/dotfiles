" vim: syntax=vim


""
" build with python 3.x:
" ./configure --enable-pythoninterp --enable-python3interp --enable-cscope \
" --enable-gui=gtk2 --with-features=big --enable-netbeans --with-tlib=ncurses
"
" pacman --noconfirm -S \
" vim-spell-en vim-spell-de vim-spell-it vim-spell-fr vim-spell-ru vim-spell-es
"
" system:
"   https://github.com/rpuntaie/rollarch
"   https://github.com/rpuntaie/dotfiles
"   - python with vim_py3_rst
"   - cscope
"   - wordnet
"   - rst2jira
"   - waf
"
" <ESC> mapped to <CAPSLOCK> in the OS
"
" doc/myvimrc.txt.stpl uses """, "": and ""| lines
""

""| Links to plugin mappings::

set encoding=utf-8
scriptencoding utf-8 " else listchars problem on Windows

set nocompatible

let mapleader = ","
let maplocalleader = '\'
noremap ` ,

if has('win32')
  let g:localvimfiles=$HOME.'/vimfiles'
  let $MYVIMRC=g:localvimfiles.'/vimrc'
else
  " to use vimrc in unconfigured system:
  " VIMINIT="so dotfiles/etc/vim/vimrc" vim
  if empty($XDG_CONFIG_HOME)
    let $XDG_CONFIG_HOME=expand('~').'/dotfiles/etc'
  endif
  if empty($XDG_STATE_HOME)
    let $XDG_STATE_HOME=expand('~').'/.cache/vim'
  endif
  let $MYVIMRC=$XDG_CONFIG_HOME.'/vim/vimrc'
  let g:localvimfiles=$XDG_STATE_HOME.'/vim'
endif
let g:bundledir=g:localvimfiles.'/dein'
set undofile
let &undodir=g:localvimfiles.'/undo'
if !has('win32')
  " XDG Environment
  " https://tlvince.com/vim-respect-xdg
  set directory=$XDG_STATE_HOME/vim,~/,/tmp
  set backupdir=$XDG_STATE_HOME/vim,~/,/tmp
  if !has('nvim')
    set viminfofile=$XDG_STATE_HOME/vim/viminfo
  endif
  set runtimepath=$XDG_CONFIG_HOME/vim,$XDG_CONFIG_HOME/vim/after,$VIM/vimfiles,$VIMRUNTIME
endif
function! s:Deinplugins()
    "download
    call dein#add('bogado/file-line') " open <file>:<line>
    call dein#add('chikamichi/mediawiki.vim') " syntax .mw .wiki .mediawiki
""| |plugin/02tlib.vim|  TScratch TBrowseOutput
    call dein#add('tomtom/tlib_vim')
""| |quickfix-reflector| enable editing the quickfix to change more files
    call dein#add('stefandtw/quickfix-reflector.vim')
""| |fzf-vim-commands| :Snippets :Helptags :Marks |myvmimaps|
    call dein#add('junegunn/fzf', { 'build': './install --all', 'merged': 0 }) 
    call dein#add('junegunn/fzf.vim', { 'depends': 'fzf' })
""| |bufexplorer| still needed to delete/copy
    call dein#add('jlanzarotta/bufexplorer')
""| |sleuth| detect tab settings heuristically, :Sleuth
    call dein#add('tpope/vim-sleuth')
""| |:Tabularize| format (,aa)
    call dein#add('godlygeek/tabular')
""| |sandwich-keymappings| s{adr} dasX da<wait>s (https://github.com/machakann/vim-sandwich/issues/62)
    call dein#add('machakann/vim-sandwich')
""| |easymotion-default-mappings| ,,{fFtTwWbB(g)eEjknN}
    call dein#add('Lokaltog/vim-easymotion') " <leader><leader>f
""| |matchup-default-mappings|  % {gv[]zai(ds)(cs}%
    call dein#add('andymass/vim-matchup')
""| |coc| intellisense using language server protocol
    call dein#add('neoclide/coc.nvim', {'merged':0, 'build': 'yarn install --frozen-lockfile'})
    "snippets
""| |UltiSnips| snippets
    call dein#add('SirVer/ultisnips')
    call dein#add('honza/vim-snippets')
    "languages
    call dein#add('sheerun/vim-polyglot')
    call dein#add('neovimhaskell/haskell-vim')
    call dein#add('Chiel92/vim-autoformat') " :Autoformat
    call dein#add('metakirby5/codi.vim') " :Codi :Codi!
""| |AleToggle| linting (:ALEToggle)
    call dein#add('w0rp/ale')
    "python
""| |vim_py3_rst| python and rst helpers
    call dein#add('rpuntaie/vim_py3_rst')
    "call dein#add('python-mode/python-mode')
""| |Nvim-R-use| \rf \rq \d ... in R file
    call dein#add('jalvesaq/Nvim-R')
""| |vimproc-commands| asynchronous execution
    call dein#add('Shougo/vimproc.vim', {'build': 'make'})
""| |vebugger-gdb| :VBGstartGDB, ` for leader
    call dein#add('idanarye/vim-vebugger')
""| |Vimspector| <F5> (needs .vimspector.json)
    call dein#add('puremourning/vimspector')
""| |vimtex-default-mappings| for latex
    call dein#add('lervag/vimtex')
""| |makeshift| [:MM] builds the current project
    call dein#add('johnsyweb/vim-makeshift') " Build
""| |NERDTreeMappings| [sn] file browser
    call dein#add('scrooloose/nerdtree')
    call dein#add('tyru/open-browser.vim')
    "commenting
""| |NERDCommenterFunctionality| ,c{ cnmisy$Aalbu}
    call dein#add('scrooloose/nerdcommenter')
    "git tools
""| |fugitive-commands| :Gxxx like :Gdiffsplit
    call dein#add('tpope/vim-fugitive')
""| |gitv-usage| :Gitv[!]
    call dein#add('gregsexton/gitv') " needs fugitive
    "other vcs
""| |vcscommand-mappings| works not only for git (<leader>z)
    call dein#add('vim-scripts/vcscommand.vim')
    "dictionary
    call dein#add('mattn/webapi-vim')
""| |trans-usage| ,tt
    call dein#add('echuraev/translate-shell.vim')
""| mathematic.vim (<leader>lm)
    call dein#add('Rykka/mathematic.vim')
""| |unicode-plugin| :UnicodeTable i: <C-X><C-G>
    call dein#add('chrisbra/unicode.vim')
""| |barbaric-functions| (ibus) input method only in insert mode
    call dein#add('rpuntaie/vim-barbaric')
"'airblade/vim-gitgutter'
"'editorconfig/editorconfig-vim'
"'itchyny/lightline.vim'
"'mattn/emmet-vim'
"'terryma/vim-multiple-cursors'
"'tpope/vim-eunuch'
"'severin-lemaignan/vim-minimap'
"ctags based 'majutsushi/tagbar'
"LSP based 'liuchengxu/vista.vim'
endfunction
let g:bundleinit=0
if empty($NO_DEIN_INSTALL) && has('vim_starting') && executable('git')
    if isdirectory(g:bundledir.'/repos')
        let g:bundleinit=1
    endif
    let g:dein_url='https://github.com/Shougo/dein.vim'
    let g:dein_path=g:bundledir.'/repos/github.com/Shougo/dein.vim'
    if g:bundleinit==0
        let g:bundleinit=2
        call mkdir(g:bundledir, 'p', 0700)
        execute '!git clone '.g:dein_url.' '.g:dein_path
    endif
    let &runtimepath=&runtimepath.','.g:dein_path
    if dein#load_state(g:bundledir)
        call dein#begin(g:bundledir)
        call dein#add(g:dein_path)

        call <SID>Deinplugins()

        call dein#end()
        call dein#save_state()
    endif
    if g:bundleinit==2
        call dein#install()
    endif
endif
"call dein#check_clean()
"call dein#update()
"call dein#install()

let g:powerline_pycmd = "py3"
"SirVer/ultisnips
let g:UltiSnipsExpandTrigger = '<tab>'
let g:UltiSnipsListSnippets = '<c-tab>'
let g:UltiSnipsJumpForwardTrigger = '<tab>'
let g:UltiSnipsJumpBackwardTrigger = '<s-tab>'
"sheerun/vim-polyglot
"https://github.com/sheerun/vim-polyglot/issues/204
let g:polyglot_disabled = ['latex']
"w0rp/ale
let g:ale_enabled=0 " :ALEToggle
"python-mode/python-mode
let g:pymode_lint = 0
let g:pymode_folding = 0
let g:pymode_lint_write = 0
"lervag/vimtex
let g:tex_flavor='latex'
let g:vimtex_view_method='zathura'
let g:vimtex_quickfix_mode=0
let g:tex_conceal='abdmg'
"johnsyweb/vim-makeshift
let g:makeshift_on_startup = 0
let g:makeshift_on_bufread = 0
let g:makeshift_on_bufnewfile = 0
let g:makeshift_on_bufenter = 0
let g:makeshift_systems = {
    \'CMakeList.txt': 'cmake',
    \'meson.build': 'meson',
    \'build.sbt ': 'sbt',
    \'dodo.py ': 'doit',
    \'boost-build.jam ': 'b2',
    \}
"scrooloose/nerdtree
let g:NERDTreeNodeDelimiter = "\u00a0"
let g:NERDTreeShowHidden=1
let g:netrw_shq="'"
let g:netrw_browsex_viewer= "xdg-open"
let g:netrw_nogx=1 " disable netrw's gx mapping.
let g:netrw_altv = 1
let g:netrw_preview = 1
let g:netrw_list_hide = "^\\..*"
let g:netrw_home = $XDG_STATE_HOME.'/vim'
let g:VCSCommandMapPrefix = "<leader>z"
"echuraev/translate-shell.vim
"Rykka/mathematic.vim
let g:mathematic_user_dir = g:bundledir.
  \ '/repos/github.com/Rykka/mathematic.vim/keymap/mathematic.vim'
"idanarye/vim-vebugger
let g:vebugger_leader='`'
let g:vebugger_view_source_cmd='edit'
"rpuntaie/vim-barbaric
let g:barbaric_default = 'xkb:us::eng'
"coc.nvim
let g:coc_global_extensions = [
      \ 'coc-dictionary',
      \ 'coc-tag',
      \ 'coc-word',
      \ 'coc-emoji',
      \ 'coc-omni',
      \ 'coc-syntax',
      \ 'coc-ultisnips',
      "\ 'coc-snippets',
      \ 'coc-python',
      \ 'coc-clangd',
      "\ 'coc-cmake',
      \ 'coc-json',
      \ 'coc-java',
      \ 'coc-tsserver',
      \ 'coc-sh',
      \ 'coc-html',
      \ 'coc-css',
      \ 'coc-svg',
      \ 'coc-vimlsp',
      "\ 'coc-godot',
      "\ 'coc-yaml',
      "\ 'coc-highlight',
      "\ 'coc-angular'
      \ ]
call coc#config('languageserver', {
      \  'haskell': {
      \    "command": "haskell-language-server-wrapper",
      \    "args": ["--lsp"],
      \    "rootPatterns": [
      \      "*.cabal",
      \      "stack.yaml",
      \      "cabal.project",
      \      "package.yaml"
      \    ],
      \    "filetypes": [
      \      "hs",
      \      "lhs",
      \      "haskell"
      \    ],
      \    "initializationOptions": {
      \      "languageServerHaskell": {
      \      }
      \    }
      \  }
      \})
"vimspector
"./install_gadget.py --basedir $XDG_STATE_HOME/vim/vimspector --all --force-all
let g:vimspector_base_dir=expand('$XDG_STATE_HOME/vim/vimspector')
nmap <F5>         <Plug>VimspectorContinue
nmap <S-F5>       <Plug>VimspectorRestart
nmap <F3>         <Plug>VimspectorStop
"nmap <S-F3>       <Plug>VimspectorReset (not working: run via command line)
nmap <F6>         <Plug>VimspectorPause
nmap <F9>         <Plug>VimspectorToggleBreakpoint
nmap <S-F9>       <Plug>VimspectorToggleConditionalBreakpoint
nmap <F8>         <Plug>VimspectorAddFunctionBreakpoint
nmap <F7>         <Plug>VimspectorStepOver
nmap <F11>        <Plug>VimspectorStepInto
nmap <F12>        <Plug>VimspectorStepOut
"using gdb
packadd termdebug
":Termdebug <app>

runtime ftplugin/man.vim
if has("gui_running")
    nnoremap K :<C-U>exe "Man" v:count "<C-R><C-W>"<CR><CR>
endif


if isdirectory($MY)
    cd $MY
endif

